import org.springframework.boot.gradle.plugin.SpringBootPlugin

import javax.tools.JavaCompiler

plugins {
    id 'org.springframework.boot' version '2.7.4' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'io.freefair.lombok' version '6.5.1' apply false
    id "org.sonarqube" version "3.5.0.2730"
    id 'groovy'
}

sonarqube {
    properties {
        property "sonar.projectKey", "all-stew_clockwork"
        property "sonar.organization", "all-stew"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


ext {
    springCloudVersion = '2021.0.1'
    springSecurityVersion = '2.7.5'

    // data
    mybatisPlusBootStarterVersion = '3.5.2'
    mysqlConnectorJavaVersion = '5.1.49'
    springRedisVersion = '2.7.5'

    // mapstruct
    mapstructVersion = '1.4.1.Final'

    jjwtVersion = '0.9.0'
}

allprojects {
    group = 'com.zhaojj11.clockwork'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'groovy'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    tasks.withType(JavaCompiler) {
        options.encoding = 'UTF-8'
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            // 一方

            // 二方

            // 三方
            // java
            dependency "javax.xml.bind:jaxb-api:2.3.0"
            // spring相关
            dependency "org.springframework.boot:spring-boot-starter-security:${springSecurityVersion}"

            // data
            dependency "com.baomidou:mybatis-plus-boot-starter:${mybatisPlusBootStarterVersion}"
            dependency "mysql:mysql-connector-java:${mysqlConnectorJavaVersion}"
            dependency "org.springframework.boot:spring-boot-starter-data-redis:${springRedisVersion}"

            // mapstruct
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"

            dependency "io.jsonwebtoken:jjwt:${jjwtVersion}"

            dependency "junit:junit:4.13.2"
        }
    }
}

